buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.4"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.google.protobuf'

eclipse.project.name = 'TodaroProject'

def grpcVersion = '1.59.1'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        proto {
            srcDir 'protobuf'
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // JUnit / Mockito
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'

    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.0'

    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // gRPC
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly     "io.grpc:grpc-netty-shaded:${grpcVersion}"
}

test {
    useJUnitPlatform()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins { 
            	java {}
        }
            task.plugins { 
            	grpc {} 
        }
        }
    }

    generatedFilesBaseDir = "$projectDir/build/generated"
}

apply plugin: 'application'

tasks.register('runDataStore', JavaExec) {
    group = "application"
    description = "Run the DataStore gRPC server"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'processapi.grpc.DataStoreServer'
    standardInput = System.in
}

tasks.register('runCompute', JavaExec) {
    group = "application"
    description = "Run the Compute gRPC server"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'networkapi.grpc.UserComputeServer'
    standardInput = System.in
}

tasks.register('runClient', JavaExec) {
    group = "application"
    description = "Run the UserComputeClient frontend"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'frontend.UserComputeClient'
    standardInput = System.in
}

tasks.register('printRuntimeClasspath') {
    doLast {
        println sourceSets.main.runtimeClasspath.asPath
    }
}